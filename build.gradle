plugins {
  id("idea")
  id("org.jetbrains.gradle.plugin.idea-ext") version "1.0.1"
  id("java")
  id("com.github.ben-manes.versions") version "0.38.0"
  id("org.jetbrains.changelog") version "1.2.1"
  id("org.jetbrains.intellij") version "0.7.3"
}

group = "com.hiberbee.themes"
version = new Date().format('yyyy.M.d.HHmm')

repositories {
  mavenCentral()
}

changelog {
  version = project.version
  path = "${project.projectDir}/CHANGELOG.md"
  header = { "[$version]" }
  headerParserRegex = ~/\d+\.\d+\.\d+\.\d+/
  itemPrefix = "-"
  keepUnreleasedSection = true
  unreleasedTerm = "[Unreleased]"
  groups = ["Added", "Changed", "Removed", "Fixed"]
}

intellij {
  pluginName = "Hiberbee theme"
  type = "IC"
  updateSinceUntilBuild = true
  configureDefaultDependencies = true
  instrumentCode = false
  downloadSources = true
}

idea {
  project {
    settings {
      copyright {
        useDefault = "MIT"
        profiles {
          MIT {
            notice = file("LICENSE").readLines().join("\n")
          }
        }
      }
    }
  }
}

publishPlugin {
  setToken(System.getenv("JETBRAINS_TOKEN"))
}

patchPluginXml {
  changeNotes({ changelog.getLatest().toHTML() })
  pluginDescription("Monokai Pro &amp; MacOS Mojave inspired color themes, with bright colors &amp; dark mode, with common color code for all languages. See description &amp; screenshots on <a href=\"https://github.com/Hiberbee/code-highlight-themes\">Github</a>. Check the sources, iTerm2 and macOS Terminal and XCode color schemes also available")
  pluginXmlFiles("src/main/resources/META-INF/plugin.xml")
  untilBuild(System.getenv("JETBRAINS_IDE_VERSION"))
  version(project.version)
}
